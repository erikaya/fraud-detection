import org.springframework.cloud.contract.verifier.config.TestFramework

buildscript {
	ext {
		contractVersion = "2.0.1.RELEASE"
	}

	repositories {
		mavenCentral()
	}

	dependencies {
		classpath "org.springframework.cloud:spring-cloud-contract-gradle-plugin:2.1.1.RELEASE"
		classpath "org.springframework.cloud:spring-cloud-contract-pact:${contractVersion}"
	}
}


plugins {
	id 'org.springframework.boot' version '2.1.5.RELEASE'
	id 'java'
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'groovy'
apply plugin: 'spring-cloud-contract'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
	mavenLocal()
}

ext {
	set('springCloudVersion', "Greenwich.SR1")
}

contracts {
	contractDependency {
		stringNotation = "${project.group}:${project.name}:${project.version}"
	}

	contractRepository {
		repositoryUrl = "git://https://github.com/erikaya/spring-cloud-contracts-poc.git"
	}

	contractsMode = "REMOTE"

	baseClassForTests = 'com.example.frauddetection.BaseClass'
	testFramework = TestFramework.JUNIT5
}

publishStubsToScm.configProperties.contractRepository.repositoryUrl = "git://https://github.com/erikaya/spring-cloud-contracts-poc.git"
publishStubsToScm.configProperties.contractRepository.username = "erikaya"
publishStubsToScm.configProperties.contractRepository.password = System.getenv('CONTRACT_REPOSITORY_PASSWORD')

publish.dependsOn("publishStubsToScm")

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compile 'org.springframework.cloud:spring-cloud-starter-stream-rabbit'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testCompile 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
	testCompile 'org.springframework.cloud:spring-cloud-stream-test-support'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}
